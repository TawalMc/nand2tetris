// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:

	 // if (zx == 1) set x = 0 
	 And16(a=x, b=false, out=andx0);
	 Mux16(a=x, b=andx0, sel=zx, out=zxx0);
	
	 // if (nx == 1) set x = !x
	 Not16(in=zxx0, out=notx);
	 Mux16(a=zxx0, b=notx, sel=nx, out=nxnotx);

	 // if (zy == 1) set y = 0
	 And16(a=y, b=false, out=andy0);
	 Mux16(a=y, b=andy0, sel=zy, out=zyy0);
   
	 // if (ny == 1) set y = !y
	 Not16(in=zyy0, out=noty);
	 Mux16(a=zyy0, b=noty, sel=ny, out=nynoty);
	 
	 // x + y
	 Add16(a=nxnotx, b=nynoty, out=xaddy);

	 // x & y
	 And16(a=nxnotx, b=nynoty, out=xandy);

	 // if (f == 1)  set out = x + y
	 // if (f == 0)  set out = x & y
	 Mux16(a=xandy, b=xaddy, sel=f, out=xcomputey);

	 // if (no == 1) set out = !out
	 // if (out < 0) set ng = 1
	 Not16(in=xcomputey, out=notxcomputey);
	 Mux16(a=xcomputey, b=notxcomputey, 
	 			sel=no, out=out, out[0..7]=out1, out[8..15]=out2, out[15]=ng);

	 // if (out == 0) set zr = 1
	 Or8Way(in=out1, out=orout1);
	 Or8Way(in=out2, out=orout2);
	 Or(a=orout1, b=orout2, out=orout8way);
	 Mux(a=true, b=false, sel=orout8way, out=zr);
}